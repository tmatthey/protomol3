import os
import sys
execfile('compiler.py')


# Command Line Options
opts = Options('options.py')
Export('opts')

opts.AddOptions(
    BoolOption('fah', 'Set to 1 to build library for Folding@Home', 0),
    BoolOption('lapack', 'Use LAPACK', 0),
    BoolOption('simtk_lapack', 'Use SimTK LAPACK', 0),
    )

compiler_add_opts()

# Create environment and import external PATH
env = Environment(options = opts, ENV = os.environ)
Export('env')


# Configure
conf = Configure(env)
Export('conf')


# Import subdir build files
subsrc = SConscript('protomol/SConscript')

# Get options
fah = int(env.get('fah', 0))
use_lapack = int(env.get('lapack', 0))
use_simtk_lapack = int(env.get('simtk_lapack', 0))


# Configure compiler
compiler_configure()


# Include protomol
env.Append(CPPPATH = '#')


# Generate Help
Help(opts.GenerateHelpText(env))


# libprotomol
libname = 'protomol'
if fah: libname = libname + '-fah'
libsrc = []
libs = []
libs.append(libname)


if not env.GetOption('clean'):
  execfile('package_h.py')
  execfile('config.py')

  if (have_bzip2 and have_boost_iostreams and have_libfah):
    env.Append(CPPDEFINES = ['HAVE_LIBFAH'])
    have_libfah=1


  # Folding@Home library version
  if fah:
    if not (have_libfah and have_bzip2 and have_boost_iostreams):
      print 'Need libfah, boost iostreams and libbzip2 for Folding@Home build'
      Exit(1)

    env.Append(CPPDEFINES = ['BUILD_FOR_FAH'])


  env = conf.Finish()


if fah:
  libsrc.append('#/protomol/modules.cpp')


# Get source
for s in subsrc:
  libsrc.append('#/protomol/' + s)

src = ['protomol/main.cpp', 'protomol/modules.cpp']

# Build libprotomol
env.Library(libname, libsrc)

# Build Protomol
env.Append(LIBPATH = ['.'])
env.Prepend(LIBS = [libname])
protomol = env.Program('ProtoMol', src)
Default(protomol)


if env['PLATFORM'] == 'win32':
  if debug: variant = 'Debug'
  else: variant = 'Release'

  src = []
  def add_hdrs(arg, dir, files):
    global src
    for file in files:
      if file.endswith(arg):
        src += [dir + os.sep + file]

  os.path.walk('protomol', add_hdrs, '.h')
  inc = src
  src = []
  os.path.walk('protomol', add_hdrs, '.cpp')

  env.MSVSProject(target = 'ProtoMol' + env['MSVSPROJECTSUFFIX'],
                  srcs = src, incs = inc,
                  auto_build_solution = 0,
                  variant = ['Debug', 'Release'],
                  buildtarget = ['debug=1', 'debug=0'])


# Tidy
env.Command('tidy', '', 'rm -f config.log $$(find . -name \*~ -o -name \#*)')
